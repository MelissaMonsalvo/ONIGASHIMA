label prologue:

    $ loop1_shiori_mandatory1 = True
    $ loop1_shiori_mandatory2 = True
    $ loop1_shiori_mandatory3 = True
    $ loop1_shiori_mandatory4 = True
    $ loop1_shiori_mandatory5 = True

    $ loop1_yamato_mandatory1 = True
    $ loop1_yamato_mandatory2 = True
    $ loop1_yamato_mandatory3 = True
    $ loop1_yamato_mandatory4 = True
    $ loop1_yamato_mandatory5 = True

    $ loop1_hikaru_mandatory1 = True
    $ loop1_hikaru_mandatory2 = True
    $ loop1_hikaru_mandatory3 = True
    $ loop1_hikaru_mandatory4 = True
    $ loop1_hikaru_mandatory5 = True

    ### this has the prologue before

    ### azure is testing fer stuff lel
    scene black with fade
    #show screen eyessss
    #n "testing fer eyes."
    show flesh2 at scary_flicker
    # particleFog with dissolve:
        #ypos 0.4

    play music "heavy breathing.mp3"

    n "{cps=18}It is dark.{/cps}"

    n "Something is caught in your throat. A {color=#ff0000}pool of blood{/color} lies at your feet."

    play sound "sfx/choke.wav"
    $ renpy.pause(0.8)

    n "Are you choking?"

    n "You shouldn’t be. You’re alive. Whole. {w=0.4}{i}Well, mostly.{/i}"

    n "And most importantly, you have won."

    play muzak "sfx/squelch.mp3"

    n "You stab again. And again. {w=0.1}Again. Again.{w=0.2} No more resistance."

    n "You hear the sound of {color=#ff0000}wet things{/color} rearranging."

    play muzak "sfx/squelch.mp3"

    n "How many times?"

    n "How many times until it stops twitching? Until your hands do?"

    n "As you grip the hilt tighter, it makes an {i}obscene{/i} sound beneath your fingers. That’s not right. {w=0.3}That’s not–"

    n "You’ll have to wash it later. Scrub hard. Maybe burn it."

    n "Congratulations. {w=0.3}You did it. {w=0.3}You did it. {w=0.3}You {b}did it.{/b}"

    n "The enemy is {atl=-#,#,fade_in_text~0.6}{glitch=1.1}DEAD.{/glitch}{/atl}"

    n "The threat is gone. Gone. {w=0.2}Gone. {w=0.2}Isn’t it?"

    n "You stab again. {w=0.2}Again. {w=0.2}Again. {w=0.2}Again. {w=0.2}Again. {w=0.2}Again. {w=0.2}Again. {w=0.3}All right. You can stop now."

    play sound "sfx/drip.wav"
    $ renpy.pause(0.4)

    stop muzak

    n "{cps=16}Silence falls, finally, as you stop stabbing.{/cps}"

    play sound "sfx/drip.wav"

    n "You tilt your head. A warm liquid drips off your chin."

    n "Blood? No, it's thicker. Syrup? Bile? Wait... {w=0.2}Does it even smell right?"

    n "You watch it fall and listen for the sound it makes when it hits the pool under your boots."

    play sound "sfx/drip.wav"

    n "{cps=10}{color=#ff0000}Drip.{/color}{/cps}"

    play sound "sfx/drip.wav"

    n "{cps=8}{color=#ff0000}Drip.{/color}{/cps}"

    n "What did you kill?"

    n "Sorry, sorry. Let me try again. {w=0.2}Let me remember..."

    n "Ah yes. The Yamakui. The oni that has terrorized your village for years."

    n "But it bled red. Aren’t onis supposed to bleed black?"

    n "And... it looked smaller than the legends said."

    n "It had {i}hands.{/i} Smooth hands, without claws. {w=0.2}Mortal hands?"

    n "No. No, that can’t be right. The sound it made when you cut through it... {w=0.3}that wasn’t–"

    n "You heard it scream, right? {w=0.2}Or did it cry?"

    n "Did it say your name with its dying breath?"

    n "You lift the blade again. About to stab it {w=0.2}again..."

    play muzak "sfx/squelch.mp3"

    n "{size=+4}{color=#990000}Stop.{/color}{/size}"

    n "{size=+2}{color=#880000}Please. Stop.{/color}{/size}"

    n "You saved the village. This was... noble."

    n "Stabbing a thing that is no longer alive {w=0.2}again, {w=0.2}again, {w=0.2}and again is not noble. That’s not what heroes do."

    n "You’re sure it’s dead now. You can go. Return to the village."

    n "Stand tall. Lift your head."

    n "You’re a hero now. A good person."

    n "You’re not going to turn around and see it still breathing."

    n "...Because it’s {atl=-#,#,fade_in_text~0.6}{glitch=1.1}DEAD.{/glitch}{/atl} {w=0.3}You made sure."

    stop muzak
    stop music

    n "{cps=12}You made sure a thousand times already.{/cps}"

    scene black with out_212

    play muzak "sfx/forest night.wav"

    n "It’s time to return to our village."

    play sound"sfx/metal drag.wav"
    $ renpy.pause(0.8)

    n "You begin your slow descent from the mountains. Your legs know the motion already, but it feels wrong for the muscles."


    n "{w=0.2}The sound of armor dragging follows behind you. It grinds the stone path as you drag it along with one hand."

    play sound"sfx/metal drag.wav"
    $ renpy.pause(0.8)

    n "The elders said that Yamakui had an armor made of stone."

    n "You decide to loot the armor because they won’t believe you killed it."

    n "Because nothing {i}else{/i} was left, you've stabbed it into oblivion."

    play sound"sfx/metal drag.wav"
    $ renpy.pause(0.8)

    n "Well, let's see the look in their eyes once they see this now."

    n "Because you did it, the unthinkable.{w=0.3} You're {i}absolutely{/i} sure."

    n "...."

    n "{w=0.2}It's quiet, isn't it?"


    n "The air feels cold and anticlimactic, even though you just fought a big battle against the village's greatest enemy."

    n "Shouldn’t it be warmer now? Shouldn’t the wind carry your heroic feats?"

    n "You imagine sunlight parting clouds, gold light on your bloodstained hands as you walk out of the forest."

    n "But in reality... {w=0.2}It's... empty. You can't even hear birds singing in this mountain."

    play sound"sfx/metal drag.wav"
    $ renpy.pause(0.8)

    n "{w=0.2}All you hear is that dragging. That awful, awful dragging, echoing where it shouldn't."

    n "Hm? What's that? {w=0.2}Why do your eyes keep flicking back?"

    n "Are you scared you’ll see something if you don’t?"

    n "It's {size=+4}{color=#990000}{atl=-#,#,fade_in_text~1.0}{glitch=1.1}DEAD.{/glitch}{/atl}{/color}{/size}"

    n "{size=*0.95}{k=-1}{w=0.2}Don't look back.{/k}{/size}"

    play sound "sfx/metal drag.wav"
    $ renpy.pause(0.8)

    n "You killed the Oni."

    n "You were told to."

    n "{w=0.2}That’s all that matters. {w=0.2}That’s all that matters. {size=*0.9}{k=-1}That’s all that–{/k}{/size}"

    play sound "sfx/metal drag.wav"
    $ renpy.pause(0.8)

    n "{w=0.3}You’re going home."

    n "{w=0.2}You’ll be a hero."

    n "{b}{cps=8}{glitch=1.1}A hero.{/glitch}{/cps}{/b}"

    stop music fadeout 1.0
    stop muzak fadeout 1.0

label village_entrance:

    scene village day with in_182:
        zoom 0.5

    n "Finally."

    n "Once you cross the village borders, everything changes."

    n "The sky looks like it splits into two once the treeline disappears... {w}The heavenly shine that you've imagined."

    show woman:
        xzoom -1
        zoom 0.35
        xalign 0.65
        ypos 0.2

        linear 0.1 ypos 0.3 zoom 0.2
    with vpunch
    play sound "sfx/bucketdrop.wav"
    $ renpy.pause(0.8)



    n "Someone sees you, drops their bucket. Everyone stops what they are doing to look at you."



    play music "Tense.ogg"

    "Woman" "{size=*0.95}{k=-1}No way...{/k}{/size}"

    hide woman
    with dissolve

    scene village day:
        zoom 0.5
        yanchor 1
        pause 0.1
        linear 2 zoom 0.7 xalign 0.6

    play sound "sfx/walk grass.mp3"

    n "You walk toward a familiar-looking elder and hands the armor out. He steps forward and examines the armor, brows furrowing."

    pause 1.0

    stop sound

    show elder:
        xzoom -1
        zoom 1
        xalign 0.65
        ypos 0.25
    with dissolve

    "Elder" "Im-Impossible..."

    "Elder" "This couldn't be–Ahem!"

    "Elder" "Yes... {w=0.4}I believe this is truly the Yamakui's armor."

    pause 0.5

    "Elder" "T-The Oni is no more!"

    with vpunch

    play sound "sfx/cheer.mp3"

    hide elder
    with dissolve

    show woman at left:
        zoom 0.5
        ypos 0.2
    with dissolve
    show man at center:
        zoom 0.5
        ypos 1.67
    with dissolve
    show oldman at right:
        zoom 0.5
        xoffset -50
        ypos 1.67
    with dissolve

    n "Suddenly, hands reach for you. Clapping on your back, with smiles on their faces."

    n "Ah, there it is."

    n "The fanfare."

    n "The light. The noise. The celebration. The praise. {w}Finally."

    n "They love you. How wonderful."

    n "They are cheering... No, they are chanting. {w}Your name."

    n "You want them to hear your name, you want it shouted from the shrine gates to the rice fields."

    jump get_player_name



label get_player_name:

    ## player name is persistent
    ## you only name player ONLY ONCE, that name is used all across all loops until true ending is unlocked
    ## you can rename mc again after you have seen the true ending
    ## let me know if this logic breaks

    # Name input screen logic
    if persistent.player_name and not persistent.seen_true_end:
        # Skip input, already set and no good ending yet
        n "..."
    else:
        # Name input only if first time or after good ending
        # $ player_name = renpy.input("Say your name out loud.", length=8)
        # $ player_name = player_name.strip()
        # if player_name == "":
        #     $ player_name = "Kagami"

        # $ persistent.player_name = player_name
        # $ persistent.seen_true_end = False  # Reset good_end status on new game

        # n "[persistent.player_name]."

        call screen name_input()

        $ persistent.player_name = persistent.player_name.strip()

        if persistent.player_name == "":
            $ persistent.player_name = "Kagami"

        $ persistent.seen_true_end = False
        n "[persistent.player_name]."


    jump after_get_name

label after_get_name:

    "Man" "But.... how? How did you do it, [persistent.player_name]?"

    MC smug "I took down that Oni in one blow! Well... maybe two! Or ten! But who's there to count?!"

    play sound "sfx/cheer.mp3"


    "Elder" "[persistent.player_name], you have done us a great service."

    stop music fadeout 0.5

    n "The others cheer again. {cps=7}But no one looks at what’s inside the armor.{/cps}"

    play music "Hope.mp3"

    show woman at shakey

    "Woman" "Great job, [persistent.player_name]-san!"

    show oldman at shakey

    "Old Man" "Proud of you, [persistent.player_name]!"

    show man at bounci

    "Man" "{w=0.2}[persistent.player_name], {w=0.2}[persistent.player_name], {w=0.2}[persistent.player_name]!"

    n "They say your name, over, and over again. Your name has become sacred, etched in legends."

    n "You like that."

    n "You {i}really, really{/i} like that."

    scene village day:
        # Start at normal crop
        anchor (0.5, 0.5)
        xpos 0.5 ypos 0.5
        xanchor 0.5 yanchor 0.5
        zoom 0.6
        linear 0.15 zoom 0.61
        linear 0.08 xpos 0.51 ypos 0.49
        linear 0.08 xpos 0.5 ypos 0.5
        # Second stumble, more pronounced
        linear 0.12 zoom 0.63
        linear 0.10 xpos 0.48 ypos 0.52
        linear 0.10 xpos 0.5 ypos 0.5
        # Hold at max zoom
        linear 0.15 zoom 0.62
        pause 0.3

    n "Later, you are pulled to stand in front of everyone, almost stumbling on your feet."

    n "You wonder why the Elder's hand is shaking when he tugs your arm."

    n "Or maybe that's your own hand that's trembling."

    show elder:
        xzoom -1
        zoom 0.8
        xalign 0.65
        ypos 0.1
    with dissolve

    "Elder" "Come. Let them see it clearly."

    n "You hold the armor up, and it glints in the sun. A red ooze is dripping down from it."

    "Elder" "Look well, all of you."

    "Elder" "{w=0.3}For years, we have lived in {i}shadow.{/i}"

    "Elder" "We have boarded our windows, whispered our prayers, and buried too many graves with {size=*0.95}no names.{/size}"

    "Elder" "The Yamakui haunted these hills longer than {i}memory itself.{/i}"

    "Elder" "{w=0.4}And yet..."

    "Elder" "{size=*1.05}[persistent.player_name]{/size} climbed that mountain and came back. {w=0.3}Where no one else {i}could.{/i}"

    play sound "sfx/cheer.mp3"

    n "The crowd breaks into cheers and applause."

    "Elder" "{w=0.4}{b}Never again{/b} will we light our fires with {i}fear.{/i}"

    "Elder" "{b}Never again{/b} will we sleep listening for {size=*0.95}footsteps.{/size}"

    "Elder" "{b}Never again{/b} will our loved ones be taken the next {i}red moon.{/i}"

    "Elder" "{b}And never again{/b} will we... {w=0.3}{i}forget.{/i}"

    "Girl" "A speech, [persistent.player_name]!"

    n "Someone shouts from the back, it might've been your friend, Shiori."


    MC happy "Thank you, everyone. I didn’t do it alone."

    MC happycl "I had your hopes, {w}your prayers, {w}your bento. Aaaand that weird old charm from our beloved shrine maiden..."

    MC happy "Still stuck in my pocket, by the way, thanks Shiori!"

    n "The girl giggled at the back."

    MC smug "But I climbed that mountain! I faced it head-on! {w}And I won!"

    MC smug "So let’s celebrate! No more fear!"


    play sound "sfx/cheer.mp3"

    n "They cheer again. They believe you. Of course they do. The armor is in your hand, after all."

    n "{cps=7}Who wouldn’t?{/cps}"

    hide elder

    with fade

    n "As the crowd partd as you walk, you hear whispering."

    $ _prev_music_volume = _preferences.volumes["music"]

    $ decrease_music_volume(0.2)
    $ renpy.block_rollback()
    pause 0.2



    show woman at midleft :
        zoom 0.5
        ypos 0.2
    with dissolve
    show man at midright:
        zoom 0.5
        ypos 0.1
    with dissolve
    show darken
    with dissolve

    "Man" "{size=*0.95}{alpha=0.8}The next Red Moon’s coming soon, isn’t it?{/alpha}{/size}"

    "Woman" "{size=*0.95}{alpha=0.8}Think we’ll be safe this time?{/alpha}{/size}"

    "Man" "{size=*0.95}{alpha=0.8}Of course we will, [persistent.player_name] killed it.{/alpha}{/size}"

    "Man" "{size=*0.95}{alpha=0.8}There’s nothing left to hide from. Right?{/alpha}{/size}"

    n "{size=*0.9}{alpha=0.5}...Right?{/alpha}{/size}"

    $ _preferences.volumes["music"] = _prev_music_volume

    hide man
    hide woman
    hide darken
    with dissolve

    n "You can still hear people chanting, laughing, celebrating."

    n "The armor still hangs limply from your hand."

    n "It feels...{w} heavier now."

    n "And that red ooze is still dripping."

    n "But, of course, you don't get far."

    n "Because Shiori, Yamato, and Hikaru immediately finds you."



    play sound "sfx/walk grass.mp3"

    show shi happy at shiori_skipp:
        xalign 0.44
    with dissolve
    pause 0.6
    stop sound
    show yam normal at yamato_zoom, left
    with dissolve
    pause 0.3
    show hik normal at hikaru_zoom, right
    with dissolve

    shiori "{bt=1}Ehehe~ [persistent.player_name]-samaaa~!{/bt}"

    n "They are your childhood friends and confidants."

    n "You grew up running through rice paddies together, shouting at the sky, skipping stones at puddles, and laughing with no care in the world."

    n "The four of you once chased the shrine cat for hours, caught the priest’s omamori instead."

    n "Shiori used to say she’d marry someone strong."

    n "Yamato wanted to guard the village gates as a samurai, sword in hand and pride in his chest."

    n "And Hikaru...{w=0.4}Hikaru never said much, always just listening to you three ramble."

    show shi happyblush
    with dissolve

    shiori "Welcome back~! You really came home! You didn’t get eaten or squished into mochi it seems~!"

    MC smugcl "Oi, give me a little credit! I’m not that easy to mash, y’know."

    show yam annoyed
    with dissolve

    yamato "Yeah? Could’ve fooled me. Runnin’ off like a dumbass without tellin’ anyone. What, too good to ask for backup?"

    show shi annoyed at shakey:
        xalign 0.44

    shiori "Mou~ Yamato, don’t be mean. [persistent.player_name]-sama’s super cool!"

    MC normal "C’mon, you both know tradition. Oni slayer’s gotta go alone."

    yamato "Tch."

    show hik happy
    with dissolve

    hikaru "Welcome home, [persistent.player_name]-san."

    shiori "Ne, ne~ Tell us! What did Yamakui look like? Was it big? Did it have horns like a cow? Eyes like fireflies? Did it-"

    hide shi normal
    show shi smug:
        zoom 0.26
        yalign -0.60
        yoffset 0
        xalign 0.44
        parallel:
            easein 0.4 zoom 0.34 yalign -0.1
            easeout 0.4 zoom 0.26 yalign -0.60

    shiori "{w=0.2}-whisper {sc=2}seeeeecrets{/sc} into your ear while it bled? Was it... {w}beautiful?"

    MC surprised "...Huh?"

    yamato "Don’t answer that."

    show shi happy with dissolve

    shiori "Ara~ What’s wrong, Yamato? Jealous of the oni?"

    hide yam normal
    show yam angry at left:

        zoom 0.23
        yalign -0.04
        xzoom 1.0
        yzoom 1.0
        yoffset 0

        linear 0.09 yoffset 65

        linear 0.08 yoffset 30

        linear 0.12 yoffset 0

    with vpunch

    yamato "Jealous?! Of a corpse?"

    MC happy "Haha! You kinda sound like you are."


    show yam annoyed at left, shakey:

        zoom 0.23
        yalign -0.04
        xzoom 1.0
        yzoom 1.0
        yoffset 0


    yamato "Tch. Shaddup."

    yamato "I'm just sayin’... Yer' too reckless to be able t' defeat the goddamn Yamakui. Not even a scratch on ya."

    show shi smug with dissolve

    shiori "You’re just grumpy ‘cause [persistent.player_name]-sama did what you couldn’t~"

    show yam angry
    with dissolve

    yamato "Shut it. I stayed behind to protect the damned village! Some of us don’t get to play th' hero."

    MC happy "Hey, hey! C’mon, don’t fight. The Yamakui's dead, I'm alive, and we're all safe now! isn't that more important?"

    n "They're bickering again. Like always."

    n "It feels just like before you left..."

    show hik worried
    with dissolve

    hikaru "...Don’t you forget something, [persistent.player_name]?"

    MC nervous "Huh? What, like a souvenir? I brought the armor, didn’t I?"

    hikaru "...I see."

    n "Hikaru's voice drops, their eyes slide slowly. From you...to the ground. {w=0.2}No-"

    $ _prev_music_volume = _preferences.volumes["music"]

    $ decrease_music_volume(0.2)
    $ renpy.block_rollback()

    show darken
    with dissolve

    play sound "sfx/suzu.mp3"

    n "To the armor."

    show hik angry
    with dissolve

    hikaru "...It's rather... {w}small for Yamakui, isn't it?"

    MC surprised "...What?"

    show shi sad
    with dissolve

    shiori "Hikaru?"

    show yam surprised
    with dissolve

    yamato "Oi. The hell’s that supposed to mean?"

    hikaru "...Forget it."

    show hik angry behind darken:

        zoom 0.231
        yoffset 460

        linear 0.4 xzoom -1 matrixcolor (BrightnessMatrix(-0.5) * TintMatrix("#1A1A1A"))

    hikaru "Excuse me."

    hide hik
    with dissolve

    play sound "sfx/walk grass.mp3"
    pause 0.4
    stop sound fadeout 1.0

    n "Hikaru turns and walks away."



    stop music fadeout 1.0

    MC sad "...Hikaru?"

    n "The others stand around awkwardly."

    shiori "Should we-{w}Should we go after Hikaru?"

    $ _preferences.volumes["music"] = _prev_music_volume

    show yam serious
    with dissolve


    yamato "Tch, Hikaru's always weird. Let 'em be."

    MC sadcl "...Yeah..."


    stop music fadeout 0.3

    n "You look down at the armor again."

    n "Your fingers ache from gripping it. You thought you cleaned it better than this."

    n "But the stain is still there."



label beforefirstloop:

    scene white_bg
    with out_182

    show petals_dense2
    show petals_scatter2

    play music "Idle.mp3"

    n "Time passes slowly."

    n "You eat. {w=0.2}You laugh. {w=0.2}You sleep. {w=0.2}You talk. {w=0.3}Repeat."

    n "You do all the things that safe people do."

    n "{cps=20}So you must be {glitch=1.1}safe{/glitch}.{/cps}"

    show shi normal:
        zoom 0.7
        xanchor 0.5 yanchor 0.0
        xpos -0.2 ypos 0.0
        alpha 0.0

        ease 0.7 xpos 0.4 alpha 1.0

    n "Shiori still skips around beside you. She's still cheerful. Always. {w=0.3}Always."

    n "But she starts to leave white flower petals at the shrine."

    n "She always welcome you with her smile already in place."

    n "You can’t tell if she is mourning.... or celebrating."

    show shi normal:
        zoom 0.7
        xanchor 0.5 yanchor 0.0
        xpos 0.4 ypos 0.0
        alpha 1.0

        ease 1.2 xpos 1.2 alpha 0.0
    pause 0.9
    hide shi

    show yam serious:
        zoom 0.7
        xanchor 0.5 yanchor 0.0
        xpos -0.2 ypos -0.1
        alpha 0.0

        ease 0.7 xpos 0.6 alpha 1.0

    n "Yamato trains every day, always alone."

    n "He only talks to you when he has to. Wasn't he always your sparring partner?"

    n "The Yamakui is {atl=-#,#,fade_in_text~0.6}{glitch=1.1}DEAD.{/glitch}{/atl} {w=0.3}Isn’t it?"

    n "So what is he still protecting them from? You don't need to train as vigorously anymore, do you?"

    n "Maybe he doesn’t believe that you killed the Yamakui."

    n "Sometimes you feel him staring. At the way you walk... or perhaps the way the villagers looks at you now."

    n "You can't tell, but one thing is for sure..."

    n "You never look back, even though you always feel his stares deep in your bones."

    show yam serious:
        zoom 0.7
        xanchor 0.5 yanchor 0.0
        xpos 0.6 ypos -0.1
        alpha 1.0

        ease 1.2 xpos 1.2 alpha 0.0
    pause 1.0
    hide yam

    n "Hikaru..."

    show hik normal:
        zoom 0.7
        xanchor 0.5 yanchor 0.0
        xpos -0.1 ypos -0.1
        alpha 0.0

        ease 0.7 xpos 0.5 alpha 1.0

    n "Hikaru sits with you sometimes, calm as ever. They don’t talk much. They never did."

    n "But their eyes..."

    n "Their eyes linger much longer than necessary. On your hands, {w=0.2}your eyes... No...{w=0.3}Your neck?"

    n "You never dare ask what they are looking for, because you don’t want the answer to it."

    show hik normal:
        zoom 0.7
        xanchor 0.5 yanchor 0.0
        xpos 0.5 ypos -0.1
        alpha 1.0

        ease 1.2 xpos 1.2 alpha 0.0
    pause 0.9
    hide hik

    n "What did you miss, [persistent.player_name]? {w=0.3}What did you not bring back down?"

    n "You used to gather together, the four of you."

    n "Under the cherry blossom tree. Right? That’s what you remember."

    n "...But the petals are gone now. No one sits there anymore."

    n "The moon is starting to change. They say it only turns red a few times a year."

    n "No one knows why."

    n "But every time it does-"

    n "{i}That’s when the Yamakui comes down.{/i}"

    n "The villagers know. You hear it in the way doors shut early, in how they say your name... It always ends with a question mark."

    n "Wondering, always wondering..."

    n "The armor is still in your room. You wrapped it in rope once you got back. Covered it, away from view."

    n "You haven’t touched it since. {w=0.4}You won’t."

    n "Why would you?"

    scene black

    stop music
    play sound "sfx/suzu.mp3"
    with sshake

    n "{cps=50}Because you killed it. The Yamakui is {atl=-#,#,fade_in_text~0.6}{glitch=1.1}DEAD.{/glitch}{/atl}"

    n "You remember the blood, the sound of bone, of the lump of flesh being stabbed {w=0.1}over and over and over...."

    n "You remember stabbing. {w=0.2}Again. {w=0.2}Again. {w=0.2}Again. {w=0.3}Yes... A thousand times."

    n "You remember it twitching before going limp completely."

    n "{alpha=0.85}That’s what victory feels like.{/alpha}"

    n "So the Red Moon shouldn’t mean anything this time."

    n "It should rise. {w=0.2}And pass."

    n "No one should vanish, or die, or scream, or–"

    n "{size=*0.95}No one should–{/size}"

    n "{size=*0.9}{alpha=0.7}They should–{/alpha}{/size}"

    n "..."

    n "But you can't stop thinking about it, right?"

    n "Why won’t you sleep?"

    n "You keep thinking it’s fine."

    n "You keep saying it {i}IS{/i} fine."

    n "{size=*0.95}Again, {w=0.5}again, {w=0.4}again, {w=0.3}again, {w=0.2}again, {w=0.1}again, {w=0.01}again–{/size}"

    MC normal "{size=*0.95}{alpha=0.8}It’s fine.{/alpha}{/size}"

    MC "{size=+4}{b}It’s FINE.{/b}{/size}"

    n "{alpha=0.6}...Why doesn’t it help?{/alpha}"


    scene black

    pause 0.3

    play sound "sfx/day change.mp3"

    centered "{color=#9a0000}{atl=0.3,drop_text~#~ 1.5, bounce_text~5}{color=#FF0000}SEVEN DAYS UNTIL THE NEXT RED MOON{/color}{/atl}{/color}"

    with fade

    pause 0.5

    stop sound

    #jump loop1_yamato_mandatory1
    jump map


    return

    #jump loop1_mechanic
